# Maintainer: me

_gitrepo='vunit'
pkgname='vunit-hdl-git'
pkgver=4.7.0.27
pkgrel=1
pkgdesc="Vunit framework for writing automated HDL testbenches"
arch=('any')
url="https://github.com/VUnit/vunit.git"
license=('GPL3')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-setuptools' 'python-wheel')
depends=('python-colorama')
conflicts=('python-vunit_hdl')
source=("git+https://github.com/VUnit/$_gitrepo.git")
sha384sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitrepo"
  sed -i 's/\(setuptools_scm\).*/\1 >= 2.0.0"/g' pyproject.toml
 git describe --tags --long | sed 's/^v\([^-]*-[^-]*\)-g.*/\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitrepo"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/$_gitrepo"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

